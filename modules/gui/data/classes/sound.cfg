{
	new_game: "def() ->commands set(voice_embargoes, {})",

	is_voice_playing: "def(custom_obj obj) ->bool
		find(obj.audio.current_sounds, strstr(value.filename, 'voices') != 0) != null
	",

	voice_embargoes: { type: "{string -> bool}", default: {} },

	have_possible_sound: "def(string sound_name) ->bool
		find(sound_choices(sound_name), value not in voice_embargoes) != null
	",

	choose_sound: "def(string sound_name) ->string|null

	choose(filter(sound_choices(sound_name), value not in voice_embargoes))

	",

	sound_choices: "def(string sound_name) ->[string]
	query_cache(global_cache(32), sound_name,
		map(filter(files, lower(value[:size(fname)]) = fname), dir[7:] + value)
		where files = query_cache(global_cache(16), dir, get_files_in_dir(dir))
		where dir = p[0]
		where fname = p[1]
		where p = [string,string]<- regex_match('sounds/' + sound_name, '(.*/)(.*)')
	)
	",

	log_sounds: { type: "bool", default: false },

	reverb: "def(custom_obj obj, string sound_name) ->commands
		map(range(16), obj.audio.sound(sound_name, {
			pos: -if(value = 0, 0.0, 0.01 + value*0.005),
			volume: 1.0 / (1.5^value),
			filters: [
				obj.audio.speed_filter({
					speed: if(index = 0, 1.0, 0.98),
				})
			],
		}).play())
	",

	prepare_sound: "def(custom_obj obj, string sound_name, null|{ embargo: int|null, delay: decimal|null } args=null) ->null|playing_sound
	if(mm_obj != null and (embargo <= 0 or mm_obj.sound_embargoes[sound_name] = null or mm_obj.sound_embargoes[sound_name] < mm_obj.cycle) and sound != null,
		mm_obj.prepare_sound(obj, sound, sound_volume(sound), delay + sound_delay(sound))
	)
		where mm_obj = find((level<- obj.level).chars, value is obj sound_master_controller)
		where delay = if(args = null, 0.0, args.delay or 0.0)
		where sound = choose_sound(sound_name)
		where embargo = if(args = null, 0, args.embargo or 0)
	",

	play_prepared_sound: "def(custom_obj obj, playing_sound sfx, string sound_name, null|{ embargo: int|null } args=null) ->commands
	[
		if(strstr(sfx.filename, 'voices') != 0,
			set(voice_embargoes[sfx.filename], true)
		),

		if(embargo > 0, [
			set(mm_obj.sound_embargoes[sound_name], mm_obj.cycle+embargo)
		]),
		
		if(log_sounds, mm_obj.log_sound(obj, sound_name, sfx.filename)),

		sfx.play(),
	]
		where mm_obj = find_or_die((level<- obj.level).chars, value is obj sound_master_controller)
		where embargo = if(args = null, 0, args.embargo or 0)
	",

	play_sound: "def(custom_obj obj, string sound_name, null|{ embargo: int|null } args=null) ->commands

	if(sfx != null, play_prepared_sound(obj, sfx, sound_name, args))

	where sfx = prepare_sound(obj, sound_name, args)
	",

	_sound_delays_written: { type: "{string -> decimal}", default: {} },
	_sound_volumes_written: { type: "{string -> decimal}", default: {} },

	set_sound_volume: "def(string fname, decimal value) ->commands
	[
	set(_sound_volumes_written[fname], value);
	write_document('data/sound-volume.cfg', (map<- get_document('data/sound-volume.cfg')) + _sound_volumes_written, ['game_dir'])
	]
	",
	
	sound_volume: "def(string fname) ->decimal
	if(fname in _sound_volumes_written, _sound_volumes_written[fname],
	   fname in volumes, volumes[fname], 1.0)
	  where volumes = {string -> decimal}<- get_document('data/sound-volume.cfg')",

	set_sound_delay_seconds: "def(string fname, decimal value) ->commands
	[
	set(_sound_delays_written[fname], value);
	write_document('data/sound-delay.cfg', (map<- get_document('data/sound-delay.cfg')) + _sound_delays_written, ['game_dir'])
	]
	",

	sound_delay: "def(string fname) ->decimal
	if(fname in _sound_delays_written, _sound_delays_written[fname],
	   fname in delays, delays[fname], 0.0)
	  where delays = {string -> decimal}<- get_document('data/sound-delay.cfg')",
}
